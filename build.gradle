plugins {
    id "com.diffplug.gradle.spotless" version "3.18.0"
}

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: 'idea'
apply plugin: 'eclipse'
apply plugin: 'signing'

group 'uk.ac.ebi.ena.webin-cli'
version '1.7.4'

tasks.withType(Javadoc).all { enabled = false }

sourceCompatibility = 1.8
targetCompatibility = 1.8

spotless {
    java {
        licenseHeader('''/*
 * Copyright 2018-2021 EMBL - European Bioinformatics Institute
 * Licensed under the Apache License, Version 2.0 (the "License"); you may not use this
 * file except in compliance with the License. You may obtain a copy of the License at
 * http://www.apache.org/licenses/LICENSE-2.0
 * Unless required by applicable law or agreed to in writing, software distributed under the
 * License is distributed on an "AS IS" BASIS, WITHOUT WARRANTIES OR
 * CONDITIONS OF ANY KIND, either express or implied. See the License for the
 * specific language governing permissions and limitations under the License.
 */
''')
        importOrder 'java', 'javax', 'org', 'com', '', 'uk'
        removeUnusedImports()
    }
}

wrapper {
    gradleVersion = '5.2.1'
}

configurations.all {

    if (!hasProperty('gitlab_private_token'))
        project.ext.properties.gitlab_private_token = ''
    else
        project.ext.properties.gitlab_private_token = gitlab_private_token;
}

dependencies {
    compile('uk.ac.ebi.biosamples:client:5.2.18-RC1')

    implementation( 'org.springframework.retry:spring-retry:1.3.0' )

    implementation('org.slf4j:slf4j-api:1.7.28')

    testImplementation('junit:junit:4.12')
    testImplementation('org.assertj:assertj-core:3.11.1')

    testRuntime("org.apache.logging.log4j:log4j-to-slf4j:2.16.0")
}

repositories {
    mavenLocal()
    mavenCentral()
    maven {
        url "https://gitlab.ebi.ac.uk/api/v4/projects/1414/packages/maven"
        credentials(HttpHeaderCredentials) {
            name = "Private-Token"
            value = project.ext.properties.gitlab_private_token
        }
        authentication {
            header(HttpHeaderAuthentication)
        }
    }

    // For BioSamples client
    maven {
        url 'https://gitlab.ebi.ac.uk/api/v4/projects/2669/packages/maven'

        credentials(HttpHeaderCredentials) {
            name = "Private-Token"
            value = biosamples_gitlab_private_token
        }
        authentication {
            header(HttpHeaderAuthentication)
        }
    }
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from 'build/docs/javadoc'
}
task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

artifacts
        {
            archives jar
            archives javadocJar
            archives sourcesJar
        }

task sourceJar(type: Jar) {
    from sourceSets.main.allJava
    archiveClassifier = 'sources'
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact sourceJar
        }
    }
    repositories {
        maven {
            url "https://gitlab.ebi.ac.uk/api/v4/projects/1414/packages/maven"
            credentials(HttpHeaderCredentials) {
                name = "Private-Token"
                value = project.ext.properties.gitlab_private_token
            }
            authentication {
                header(HttpHeaderAuthentication)
            }
        }
    }
}

test {
    // Webin account
    if( System.env.webin_password ) {
        environment 'webin-password', System.env.webin_password
    } else {
        environment 'webin-password', webinPassword
    }

    if( System.env.webin_username ) {
        environment 'webin-username', System.env.webin_username
    } else {
        environment 'webin-username', webinUsername
    }

    // Biosamples Webin account
    if( System.env.biosamples_webin_password ) {
        environment 'biosamples-webin-password', System.env.biosamples_webin_password
    } else {
        environment 'biosamples-webin-password', biosamplesWebinPassword
    }

    if( System.env.biosamples_webin_username ) {
        environment 'biosamples-webin-username', System.env.biosamples_webin_username
    } else {
        environment 'biosamples-webin-username', biosamplesWebinUsername
    }
}
